h4. Simple Variables

The previous section demonstrated how to map simple URLs with concrete "tokens". In URL mapping speak tokens are the sequence of characters between each slash, '/'. A concrete token is one which is well defined such as as @/product@. However, in many circumstances you don't know what the value of a particular token will be until runtime. In this case you can use variable placeholders within the URL for example:

{code:java}
static mappings = {
  "/product/$id"(controller: "product")
}
{code}

In this case by embedding a $id variable as the second token Grails will automatically map the second token into a parameter (available via the [params|controllers] object) called @id@. For example given the URL @/product/MacBook@, the following code will render "MacBook" to the response:

{code:java}
class ProductController {
     def index() { render params.id }
}
{code}

You can of course construct more complex examples of mappings. For example the traditional blog URL format could be mapped as follows:

{code:java}
static mappings = {
   "/$blog/$year/$month/$day/$id"(controller: "blog", action: "show")
}
{code}

The above mapping would let you do things like:

{code:java}
/graemerocher/2007/01/10/my_funky_blog_entry
{code}

The individual tokens in the URL would again be mapped into the [params|controllers] object with values available for @year@, @month@, @day@, @id@ and so on.

h4. Dynamic Controller and Action Names

Variables can also be used to dynamically construct the controller and action name. In fact the default Grails URL mappings use this technique:

{code:java}
static mappings = {
    "/$controller/$action?/$id?"()
}
{code}

Here the name of the controller, action and id are implicitly obtained from the variables @controller@, @action@ and @id@ embedded within the URL.

You can also resolve the controller name and action name to execute dynamically using a closure:

{code:java}
static mappings = {
    "/$controller" {
        action = { params.goHere }
    }
}
{code}

h4. Optional Variables

Another characteristic of the default mapping is the ability to append a ? at the end of a variable to make it an optional token. In a further example this technique could be applied to the blog URL mapping to have more flexible linking:

{code:java}
static mappings = {
    "/$blog/$year?/$month?/$day?/$id?"(controller:"blog", action:"show")
}
{code}

With this mapping all of these URLs would match with only the relevant parameters being populated in the [params|controllers] object:

bc.
/graemerocher/2007/01/10/my_funky_blog_entry
/graemerocher/2007/01/10
/graemerocher/2007/01
/graemerocher/2007
/graemerocher

h4. Optional File Extensions

If you wish to capture the extension of a particular path, then a special case mapping exists:

{code}
"/$controller/$action?/$id?(.$format)?"()
{code}

By adding the @(.$format)?@ mapping you can access the file extension using the @response.format@ property in a controller:

{code} 
def index() {
    render "extension is ${response.format}"
}
{code}

h4. Arbitrary Variables

You can also pass arbitrary parameters from the URL mapping into the controller by just setting them in the block passed to the mapping:

{code:java}
"/holiday/win" {
     id = "Marrakech"
     year = 2007
}
{code}

This variables will be available within the [params|controllers] object passed to the controller.

h4. Dynamically Resolved Variables

The hard coded arbitrary variables are useful, but sometimes you need to calculate the name of the variable based on runtime factors. This is also possible by assigning a block to the variable name:

{code:java}
"/holiday/win" {
     id = { params.id }
     isEligible = { session.user != null } // must be logged in
}
{code}

In the above case the code within the blocks is resolved when the URL is actually matched and hence can be used in combination with all sorts of logic.
